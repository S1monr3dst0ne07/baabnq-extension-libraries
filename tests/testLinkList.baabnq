
use 'libs/tests/common.baabnq';


lab Local::TestLinkList::Init;
    use 'ext/linklist.baabnq';
return;






lab TestLinkList::Link::Create;
    sub Local::TestLinkList::Init;

    sub LinkList::Link::Create;
    pull _pLink;
    
    push _pLink;
    sub Chunk::Len;
    pull _len;
    jump fail ~ _len != 3;

    jump succ;


lab TestLinkList::Link::Delete;
    sub Local::TestLinkList::Init;

    sub LinkList::Link::Create;
    sub LinkList::Link::Delete;

    jump succ;
    

lab TestLinkList::Link::Next;
    sub Local::TestLinkList::Init;

    sub LinkList::Link::Create;
    pull _pLink;

    put 12345 -> _pLink + _LinkList::Link::NEXT;

    push _pLink;
    sub LinkList::Link::Next;
    pull _pNext;

    jump fail ~ _pNext != 12345;

    jump succ;


lab TestLinkList::Link::Prev;
    sub Local::TestLinkList::Init;

    sub LinkList::Link::Create;
    pull _pLink;

    put 23456 -> _pLink + _LinkList::Link::PREV;

    push _pLink;
    sub LinkList::Link::Prev;
    pull _pPrev;

    jump fail ~ _pPrev != 23456;

    jump succ;




lab TestLinkList::Link::Get;
    sub Local::TestLinkList::Init;

    sub LinkList::Link::Create;
    pull _pLink;

    put 34567 -> _pLink + _LinkList::Link::DATA;

    push _pLink;
    sub LinkList::Link::Get;
    pull _data;

    jump fail ~ _data != 34567;

    jump succ;




lab TestLinkList::Link::Set;
    sub Local::TestLinkList::Init;

    sub LinkList::Link::Create;
    pull _pLink;

    push _pLink;
    push 45678;
    sub LinkList::Link::Set;

    put _data <- _pLink + _LinkList::Link::DATA;

    jump fail ~ _data != 45678;

    jump succ;



lab TestLinkList::Link::Hook;
    sub Local::TestLinkList::Init;

    sub LinkList::Link::Create;
    sub LinkList::Link::Create;
    sub LinkList::Link::Create;

    pull _pLink; pull _pPrev; pull _pNext;

    "hook _pPrev to _pNext
    push _pNext;
    push _pPrev;
    sub LinkList::Link::Hook;

    "verify structure
    put __ <- _pPrev + _LinkList::Link::PREV; jump fail ~ __ != 0;
    put __ <- _pPrev + _LinkList::Link::NEXT; jump fail ~ __ != _pNext;

    put __ <- _pNext + _LinkList::Link::PREV; jump fail ~ __ != _pPrev;
    put __ <- _pNext + _LinkList::Link::NEXT; jump fail ~ __ != 0;


    "hook _pLink into the middle
    push _pLink;
    push _pPrev;
    sub LinkList::Link::Hook;

    "verify structure
    put __ <- _pPrev + _LinkList::Link::PREV; jump fail ~ __ != 0;
    put __ <- _pPrev + _LinkList::Link::NEXT; jump fail ~ __ != _pLink;

    put __ <- _pLink + _LinkList::Link::PREV; jump fail ~ __ != _pPrev;
    put __ <- _pLink + _LinkList::Link::NEXT; jump fail ~ __ != _pNext;

    put __ <- _pNext + _LinkList::Link::PREV; jump fail ~ __ != _pLink;
    put __ <- _pNext + _LinkList::Link::NEXT; jump fail ~ __ != 0;


    jump succ;




lab TestLinkList::Link::Unhook;
    sub Local::TestLinkList::Init;

    sub LinkList::Link::Create;
    sub LinkList::Link::Create;
    sub LinkList::Link::Create;

    pull _pLink; pull _pPrev; pull _pNext;

    push _pNext;
    push _pPrev;
    sub LinkList::Link::Hook;

    push _pLink;
    push _pPrev;
    sub LinkList::Link::Hook;

    "all the above is verified to work by TestLinkList::Link::Hook

    push _pLink;
    sub LinkList::Link::Unhook;

    "verify structure
    put __ <- _pPrev + _LinkList::Link::PREV; jump fail ~ __ != 0;
    put __ <- _pPrev + _LinkList::Link::NEXT; jump fail ~ __ != _pNext;

    put __ <- _pLink + _LinkList::Link::PREV; jump fail ~ __ != 0;
    put __ <- _pLink + _LinkList::Link::NEXT; jump fail ~ __ != 0;

    put __ <- _pNext + _LinkList::Link::PREV; jump fail ~ __ != _pPrev;
    put __ <- _pNext + _LinkList::Link::NEXT; jump fail ~ __ != 0;

    jump succ;


