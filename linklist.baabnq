use 'libs/chunk.baabnq';
use 'libs/no.baabnq';

"node interface
" _p+1 -> next :: Ptr
" _p+2 -> prev :: Ptr

put _LinkList::Node::NEXT = 1;
put _LinkList::Node::PREV = 2;

"hook structure implements node
" _p+0 -> size :: Int
" _p+1 -> head :: Ptr
" _p+2 -> tail :: Ptr

put _LinkList::Hook::SIZE = 0;

"element structure implements node
" _p+0 -> data :: Void 
" _p+1 -> next :: Ptr
" _p+2 -> prev :: Ptr

put _LinkList::Element::DATA = 0;


"This module defines a circular double linked list, which always includes
"the hook node, that acts as the point of contact for the rest of the list.
"The other nodes function as the actual elements, storing data.
"Symmetry between the hook and the elements is given by the node interface, 
"which is implemented by both.

"Parameter names are not given, as this library is analogous to c++'s std::list



jump LinkList::Skip;


"=> (::Hook)
lab LinkList::LocalAllocHook;
"=> (::Element)
lab LinkList::LocalAllocElement;
    new 3 _pNode;
    
    push _pNode;
    sub Stack::Swap;
    return;
    
    
"=> (::LinkList)
lab LinkList::CreateEmpty;
    sub LinkList::LocalAllocHook;
    pull _pList;
    
    put _pList -> _pList + _LinkList::Node::PREV;
    put _pList -> _pList + _LinkList::Node::NEXT;
    
    push _pList;
    sub Stack::Swap;
    return;
    
    
    
    

"(::LinkList) => (Head::Node)
lab LinkList::GetHeadNode;
    sub Stack::Swap;
    pull _pList;
    
    put _pHead <- _pList + _LinkList::Node::NEXT;

    push _pHead;
    sub Stack::Swap;
    return;

"(::LinkList) => (Tail::Node)
lab LinkList::GetHeadNode;
    sub Stack::Swap;
    pull _pList;
    
    put _pTail <- _pList + _LinkList::Node::PREV;

    push _pTail;
    sub Stack::Swap;
    return;
    




"(::Element) => (Data::Void)
lab LinkList::GetElementData;
    sub Stack::Swap;
    pull _pElement;
    
    put _data <- _pElement + _LinkList::Element::DATA;
    
    push _data;
    sub Stack::Swap;
    return;
    
"(::Element, Data::Void)
lab LinkList::SetElementData;
    sub Stack::Swap; pull _data;
    sub Stack::Swap; pull _pElement;
    
    put _data -> _pElement + _LinkList::Element::DATA;
    
    return;




"(::Node) => (::Node)
lab LinkList::PrevNode;
    sub Stack::Swap;
    pull _pNode;
    
    put _pPrev <- _pNode + _LinkList::Node::PREV;
    
    push _pPrev;
    sub Stack::Swap;
    return;
    
"(::Node) => (::Node)
lab LinkList::NextNode;
    sub Stack::Swap;
    pull _pNode;
    
    put _pNext <- _pNode + _LinkList::Node::NEXT;
    
    push _pNext;
    sub Stack::Swap;
    return;






"(::LinkList) => (Size::Int)
lab LinkList::Size;
    sub Stack::Swap;
    pull _pList;
    put _size <- _pList + _LinkList::Hook::SIZE;
    push _size;
    sub Stack::Swap;
    return;
    
"(::LinkList) => (Empty::Bool)
lab LinkList::IsEmpty;
    sub Stack::Swap;
    sub LinkList::Size;
    pull _size;
    
    jump LinkList::IsEmpty::False ~ _size == 0; push 0; lab LinkList::IsEmpty::False;
    jump LinkList::IsEmpty::True  ~ _size != 0; push 1; lab LinkList::IsEmpty::True;
    
    sub Stack::Swap;
    return;




"(::LinkList, Index::Int) => (::Node)
lab LinkList::GetNodeAtIndex;
    sub Stack::Swap; pull _index;
    sub Stack::Swap; pull _pList;

    push _pList;
    sub LinkList::GetHeadNode;
    pull _pIter;

    lab LinkList::GetNodeAtIndex::Loop;
        jump LinkList::GetNodeAtIndex::Done ~ _index == 0;

        put _pIter <- _pIter + _LinkList::Node::NEXT;
        put _index = _index - 1;
        jump LinkList::GetNodeAtIndex::Loop;
    lab LinkList::GetNodeAtIndex::Done;


    push _pIter;
    sub Stack::Swap;
    return;







lab LinkList::Skip;

